[Actions]
; common Values for (un)installation
include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
	;Stop process and set installation status to failed
else
	if FileExists("%ScriptPath%\delsub64.opsiscript")
		comment "Start uninstall sub section"
		Sub "%ScriptPath%\delsub64.opsiscript"
	endif
	
	Message "Installing %installingProdName% (%installingProdVersion%) 64 Bit..."
	comment "Starting the setup program"
	Winbatch_install
	Set $ExitCode$ = getLastExitCode
	if stringToBool(isInnoExitcodeFatal($ExitCode$, "True", $ErrorString$))
		isFatalError
	endif
	
	comment "Test for installation success"
	Set $ResultList$ = SearchRegistryUninstallKeys($ProductName$)
	if ( count ($ResultList$) = "0" )
		logError "Fatal: After Installation " + $ProductName$ + " could not be found in registry."
		isFatalError
	else
		Set $RegId$ = takeString (0,splitString(takeString(0,$ResultList$), '|'))
		Set $RegBitScope$ = takeString (1,splitString(takeString(0,$ResultList$), '|'))
		
		comment "Get 'InstallLocation' from registry for path to exe"
		Set $InstallDir$ = GetRegistryValue($RegId$, "InstallLocation", $RegBitScope$)
		
		comment "Check version"
		Set $PathToExe$ = $InstallDir$ + $Exe$
		
		if FileExists($PathToExe$)
			set $FileInfoList$ = getFileInfoMap($PathToExe$)
			set $InstalledVersion$ = getValue("file version with dots", $FileInfoList$)
		else
			logError "Fatal: cannot find $PathToExe$ after installation"
			isFatalError
		endif
		if contains($InstalledVersion$, $ProductVersion$)
			comment "Successful Installation"
		else
			logError "Fatal: expected version "+ $ProductVersion$ + ", but got " + $InstalledVersion$
			isFatalError
		endif
		
		if $DesktopLink$ = "true"
			comment "Create common desktop link"
			Linkfolder_create_desktoplink
		endif
		
		comment "include custom post install file"
		set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
		if not ($CustomPostInstall$ = "none")
			if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
				include_insert "%ScriptPath%\custom\" + $CustomPostInstall$	
			endif
		endif
	endif
endif

[Winbatch_install]
"%ScriptPath%\$InstFile$" /sp- $SilentOption$ /norestart /nocancel /SUPPRESSMSGBOXES

[Linkfolder_create_desktoplink]
set_basefolder common_desktopdirectory
set_subfolder ""
set_link
	name: $ProductName$
	target: $PathToExe$
end_link